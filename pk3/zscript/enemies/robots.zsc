Class RocketTurret : Actor{ //Enemigo
	default{
		Radius 20;
		Height 24;
		Speed 0;
		Health 300;
		Monster;
		+MOVEWITHSECTOR;
		+NOBLOOD;
		+AMBUSH;
		+MISSILEMORE;
		+DONTHARMCLASS;
		+DONTTHRUST;
		+NOPAIN;
		+NOINFIGHTING;
		Obituary "%o was blown to pieces by a Rocket Turret";
		SeeSound "RTurret/Activate";
		DeathSound "RTurret/Explode";
	}
	States{
		Spawn:
			RTUR A 5 A_Look;
			Loop;
		See:
			RTUR A 2 A_Chase;
			Loop;
		Missile:
			RTUR A 2 A_FaceTarget;
			RTUR B 5 Bright A_SpawnProjectile("RTurretProjectile",20,0,0);
			RTUR A 12;
			Goto See;
		Death:
			RTUR A 2 A_Scream;
			RTUR C 4 A_SpawnItemEx("DeathExplosionSmaller",0,0,20,0,0,0);
			RTUR C 2 A_Fall;
			RTUR C -1;
			Stop;
		}
}

//Cohete de la torreta de cohetes
Class RTurretProjectile : Actor{ //Proyectil
	default{
		Radius 10;
		Height 14;
		Speed 24;
		Damage 10;
		Projectile;
		Scale 0.8;
		+STRIFEDAMAGE;
		+DEHEXPLOSION;
		SeeSound "TRocket/Fire";
		DeathSound "TRocket/Explode";
		Decal "Scorch";
	}
	States{
		Spawn:
			TMIS A 2 A_PlaySound("TRocket/Flight",5,0.4,true);
			TMIS A 2 A_SpawnItemEx("RocketTrail",0,0,0,0,0,0,180);
			Loop;
		Death:
			TNT1 A 0 A_StopSound(5);
			TMIS B 5 Bright A_Explode(24,120);
			TMIS CD 4 Bright;
			Stop;
	}
}
Class DeathExplosionSmaller : Actor{ //FX
	default{
		+NOGRAVITY;
		+NOINTERACTION;
		Renderstyle "Add";
		Scale 0.65;
	}
	States{
		Spawn:
			DBOM ABCDEFGHIJK 3 Bright;
			Stop;
	}
}

///////////////////////SENTRY TURRET////////////////////////////////////////////
Class SentryGun : Actor{ //Enemigo
	default{
		obituary "%o was ventilated by an auto-sentry.";
		health 200;
		radius 20;
		height 56;
		mass 100000;
		speed 0;
		MaxTargetRange 2048;
		attacksound "sentry/attack";
		MONSTER;
		+FLOORCLIP;	
		+NOBLOOD;
		+LOOKALLAROUND;
		+STANDSTILL;
		+NOINFIGHTING;
	}
	states{
		Spawn:
			SENT AAAAAAAAAAAAAAAA 4 A_Look;
			loop;
		See:
			SENT AAAAAAAAAAAAAAAA 4 A_Chase;
			SENT A 0 A_StartSound("Sentry/Active");
			loop;
		Missile:
			SENT A 16 A_FaceTarget;
			SENT B 2 bright A_CPosAttack;
			SENT A 2 A_CposRefire;
			goto Missile+1;
		Death:
			SENT C 1 A_StartSound("Sentry/Death", 5);
			SENT C 0 A_StartSound("weapons/rocklx", 6);
			SENT C 0 A_SpawnItem("SentryBoom", 0, 48);
			SENT C 512;
		FadeOut:
			SENT C 2 A_FadeOut (0.1);
			loop;
	}
}

Class SentryBoom : Actor{ //FX
	default{
		radius 2;
		height 2;
		renderstyle "Add";
		+NOINTERACTION;
		+CLIENTSIDEONLY;
	}
	states{
		Spawn:
			XPL1 ABCDEF 3 bright;
			stop;
	}
}