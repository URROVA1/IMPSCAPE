// ========== CLASE DE BASE ==========

class UACCyber_Base : actor{

	//Todas las funciones de los diferentes jefes
	void _CircleBullets (string ProjClass, int step){
		for (int i = 0; i<360; i+=step){
			A_SpawnProjectile(ProjClass,32.0,0.0, i, CMF_ABSOLUTEPITCH|CMF_AIMDIRECTION);
		}
	}
	
	void _CacoWall(int addAngle){
			A_SpawnProjectile("UACCyber_CacodemonBallButFaster",32.0,0.0,angle+addAngle,CMF_ABSOLUTEPITCH|CMF_ABSOLUTEANGLE,0);
			A_SpawnProjectile("UACCyber_CacodemonBallButFaster",32.0,0.0,angle+addAngle,CMF_ABSOLUTEPITCH|CMF_ABSOLUTEANGLE,-10);
			A_SpawnProjectile("UACCyber_CacodemonBallButFaster",32.0,0.0,angle+addAngle,CMF_ABSOLUTEPITCH|CMF_ABSOLUTEANGLE,-20);
			A_SpawnProjectile("UACCyber_CacodemonBallButFaster",32.0,0.0,angle+addAngle,CMF_ABSOLUTEPITCH|CMF_ABSOLUTEANGLE,-30);
	}

	void _CacoBarrier(){
		for (int i = 20; i < 80; i+=20){
		_CacoWall(i);
		}
		for (int i = -20; i > -80; i-=20){
		_CacoWall(i);
		}
	}
	
	void _SpawnItemTID(string item, int tid){
		A_SpawnItemEx(item,0,0,0,0,0,0,angle,0,0,tid);
	}
	
	default{
		Radius 40;
		Height 110;
		Mass 1000000;
		Speed 16;
		PainChance 0;
		Translation "Ice";
		MONSTER;
		+NOTARGET;
		+FLOORCLIP;
		+NORADIUSDMG;
		+BOSS;
		+MISSILEEVENMORE;
		+DONTTHRUST;
		SeeSound "cyber/sight";
	}
	States{
		See:
			ANNI A 3 A_Playsound("UACCyber/Hoof");
			ANNI ABBCC 3 A_Chase();
			ANNI D 3 A_Playsound("UACCyber/Hoof");
			ANNI D 3 A_Chase();
			Loop;
	}
}

// ========== Fases ==========

class UACCyber_Phase1 : UACCyber_Base{
	States{
		Spawn:
			ANNI AB 10 A_Look();
			Loop;
		Missile:
			ANNI E 6 A_FaceTarget();
			ANNI FFFFFFFFFF 2 Bright _CircleBullets("PlasmaBall", 20);
			ANNI E 5;
			ANNI F 5 Bright _CircleBullets("UACCyber_AngularVelocityBullet_Right", 25);
			ANNI E 5;
			ANNI F 5 Bright _CircleBullets("UACCyber_AngularVelocityBullet_Left", 25);
			ANNI E 5;
			ANNI F 5 Bright _CircleBullets("UACCyber_AngularVelocityBullet_Right", 25);
			ANNI E 5;
			ANNI F 5 Bright _CircleBullets("UACCyber_AngularVelocityBullet_Left", 25);
			goto see;
		Death:
			ANNI E 0 A_Fall;
			ANNI EEE 0 A_SpawnProjectile("GravityExplosionFire",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,random(0,-60));
			ANNI EEEEEEEEEE 3 A_SpawnProjectile("ExplosionFX",random(0,128),random(-32,32),random(0,359),0);
			TNT1 A 0 _SpawnItemTID("UACCyber_Phase2",6667);
			stop;
	}
}

class UACCyber_Phase2 : UACCyber_Base{
	States{
		Spawn:
			ANNI AB 10 A_Look();
			Loop;
		Missile:
			ANNI E 6 A_FaceTarget();
			ANNI FFFFFFFFFF 1 _CacoBarrier();
			ANNI F 0 Bright{
			A_SpawnProjectile("UACCyber_BulletSpawner1",32.0,0.0,0,CMF_ABSOLUTEPITCH,-50);
			A_SpawnProjectile("UACCyber_BulletSpawner1",32.0,0.0,90,CMF_ABSOLUTEPITCH,-50);
			A_SpawnProjectile("UACCyber_BulletSpawner1",32.0,0.0,180,CMF_ABSOLUTEPITCH,-50);
			A_SpawnProjectile("UACCyber_BulletSpawner1",32.0,0.0,270,CMF_ABSOLUTEPITCH,-50);
			}
			ANNI FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 1 _CacoBarrier();
			goto see;
		Death:
			ANNI E 0 A_Fall;
			ANNI EEE 0 A_SpawnProjectile("GravityExplosionFire",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,random(0,-60));
			ANNI EEEEEEEEEE 3 A_SpawnProjectile("ExplosionFX",random(0,128),random(-32,32),random(0,359),0);
			TNT1 A 0 _SpawnItemTID("UACCyber_Phase3",6668);
			stop;
	}
}

class UACCyber_Phase3 : UACCyber_Base{
	States{
		Spawn:
		See:
			ANNI EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 2{
				A_SpawnProjectile("PixelParticleLong",256,random(-64,64),random(0,359),CMF_ABSOLUTEPITCH,90);
				A_FaceTarget();
			}
			goto missile;
		Missile:
			ANNI FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 1 {
				_CircleBullets("UACCyber_CacodemonBallButFaster",30);
				A_SetAngle(angle+0.5);
			}	
			ANNI F 0{
			A_SpawnProjectile("UACCyber_BulletSpawner1",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,-50);
			}
			loop;
		Death:
			ANNI E 0 A_Fall;
			ANNI EEE 0 A_SpawnProjectile("GravityExplosionFire",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,random(0,-60));
			ANNI EEEEEEEEEE 3 A_SpawnProjectile("ExplosionFX",random(0,128),random(-32,32),random(0,359),0);
			TNT1 A 0 _SpawnItemTID("UACCyber_Phase4",6668);
			stop;
	}
}

class UACCyber_Phase4 : UACCyber_Base{
	default{
		health 3000;
	}
	States{
		Spawn:
		See:
			ANNI EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 2{
				A_SpawnProjectile("PixelParticleLong",256,random(-64,64),random(0,359),CMF_ABSOLUTEPITCH,90);
				A_FaceTarget();
			}
			goto missile;
		Missile:
			ANNI F 1 {
				//Balas de abajo
				int i;
				for (i = 0; i<2; i++){
				A_SpawnProjectile("BaronBall_Slow",16,0,random(0,359),CMF_ABSOLUTEPITCH,0);
				}
				//Balas del medio
				for (i = 0; i<3; i++){
				A_SpawnProjectile("BaronBall_Normal",16,0,random(0,359),CMF_ABSOLUTEPITCH,-10);
				}
				//Balas de arriba
				for (i = 0; i<4; i++){
				A_SpawnProjectile("BaronBall_Fast",16,0,random(0,359),CMF_ABSOLUTEPITCH,-20);
				}
				//Balas de arriba de arriba xD
				for (i = 0; i<5; i++){
				A_SpawnProjectile("BaronBall_SuperFast",16,0,random(0,359),CMF_ABSOLUTEPITCH,-30);
				}
				//Balas de arriba de arriba de arriba xD xD xD
				for (i = 0; i<6; i++){
				A_SpawnProjectile("BaronBall_SuperSuperFast",16,0,random(0,359),CMF_ABSOLUTEPITCH,-40);
				}
			}
			loop;
		Death:
			ANNI E 0 A_Fall;
			ANNI EEE 0 A_SpawnProjectile("GravityExplosionFire",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,random(0,-60));
			ANNI EEEEEEEEEE 5 A_SpawnProjectile("ExplosionFX",random(0,128),random(-32,32),random(0,359),0);
			ANNI EEE 0 A_SpawnProjectile("GravityExplosionFire",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,random(0,-60));
			ANNI EEEEE 5 A_SpawnProjectile("ExplosionFX",random(0,128),random(-32,32),random(0,359),0);
			ANNI EEE 0 A_SpawnProjectile("GravityExplosionFire",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,random(0,-60));
			ANNI EEE 5 A_SpawnProjectile("ExplosionFX",random(0,128),random(-32,32),random(0,359),0);
			ANNI E 0 {
			A_SpawnItem("GiantExplosionFX");
			ACS_NamedExecute("BossDeath");
			}
			ANNI EEEEEEEEEE 0 A_SpawnProjectile("GravityExplosionFire",32.0,0.0,random(0,359),CMF_ABSOLUTEPITCH,random(0,-60));
			stop;
	}
}