//Barril que se puede agarrar y lanzar (da el arma ExplosiveBarrelWeapon)
class DraggableExplosiveBarrel : SwitchableDecoration{
	default{
		Health 20;
		Radius 10;
		Height 42;
		+SOLID;
		+SHOOTABLE;
		+NOBLOOD;
		+ACTIVATEMCROSS;
		+DONTGIB;
		+NOICEDEATH;
		+OLDRADIUSDMG;
		DeathSound "world/barrelx";
		Obituary "$OB_BARREL";
		Activation THINGSPEC_Switch;
		+USESPECIAL;
	}
	states{
		Spawn:
			BAR1 AB 6;
			Loop;
		Death:
			BEXP A 5 Bright;
			BEXP B 5 Bright A_Scream;
			BEXP C 5 Bright;
			BEXP D 5 Bright A_Explode;
			BEXP E 10 Bright;
			TNT1 A 1050 Bright A_BarrelDestroy;
			TNT1 A 5 A_Respawn;
			Wait;
		Active:
			TNT1 A 1{
				if (CheckInventory("ExplosiveBarrelWeapon",1,AAPTR_PLAYER1) || CheckInventory("OilDrumWeapon",1,AAPTR_PLAYER1)){
					return;
				}
				//Le saca al jugador cualquier tipo de brazo de imp que tenga.
				if (CheckInventory("ImpArm",1,AAPTR_PLAYER1))
					A_TakeInventory("ImpArm",1,0,AAPTR_PLAYER1);
					
				if (CheckInventory("DarkImpArm",1,AAPTR_PLAYER1))
					A_TakeInventory("DarkImpArm",1,0,AAPTR_PLAYER1);
					
				A_GiveInventory("ExplosiveBarrelWeapon",0,AAPTR_PLAYER1);
				A_BarrelDestroy();
			}
			goto Spawn;
		Inactive:
			TNT1 A 1{
				if (CheckInventory("ExplosiveBarrelWeapon",1,AAPTR_PLAYER1) || CheckInventory("OilDrumWeapon",1,AAPTR_PLAYER1)){
					return;
				}
				if (CheckInventory("ImpArm",1,AAPTR_PLAYER1))
					A_TakeInventory("ImpArm",1,0,AAPTR_PLAYER1);
					
				if (CheckInventory("DarkImpArm",1,AAPTR_PLAYER1))
					A_TakeInventory("DarkImpArm",1,0,AAPTR_PLAYER1);
				A_GiveInventory("ExplosiveBarrelWeapon",0,AAPTR_PLAYER1);
				A_BarrelDestroy();
			}
			goto Spawn;
	}
}

//Arma
class ExplosiveBarrelWeapon : DoomWeapon{
	default{
		Height 20;
		Weapon.SelectionOrder 2800;
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		Weapon.YAdjust 16;
		+WEAPON.NOAUTOFIRE;
	}
	states{
		Ready:
			BEXW ABCDCB 1 A_WeaponReady;
			Loop;
		Deselect:
			BEXW A 1 A_Lower;
			Loop;
		Select:
			BEXW A 1 A_Raise;
			Loop;
		Fire:
			BEXW A 1 {
			Actor pPlayer = GetPointer(AAPTR_PLAYER1);
			
			A_FireProjectile("ExplosiveBarrelProjectile");
			
			if (pPlayer is "ImpPlayer" || pPlayer is "FlyingImpPlayer")
				A_GiveInventory("ImpArm",1,AAPTR_PLAYER1);
				
			if (pPlayer is "DarkImpPlayer")
				A_GiveInventory("DarkImpArm",1,AAPTR_PLAYER1);
				
			A_TakeInventory("ExplosiveBarrelWeapon",1,0,AAPTR_PLAYER1);
			}
			TNT1 A 0;
		Spawn:
			BEXW A -1;
			Stop;
	}
}

//Proyectil
class ExplosiveBarrelProjectile : Actor{
	default{
		Health 20;
		Radius 10;
		Height 42;
		Projectile;
		-NOGRAVITY;
		+NODAMAGETHRUST;
		Speed 30;
		Damage 20;
		DeathSound "world/barrelx";
	}
	states{
		Spawn:
			BEXP AB 6 Bright;
			Loop;
		Death:
			BEXP B 5 Bright A_Scream;
			BEXP C 5 Bright;
			BEXP D 5 Bright {
			A_Explode();
			A_RadiusThrust();
			}
			BEXP E 10 Bright;
			TNT1 A 1050 Bright A_BarrelDestroy;
			Stop;
	}
}