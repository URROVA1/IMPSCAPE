
////////// IMP PLAYER //////////
class ImpPlayer : ImpscapePlayerPawn{
	default{
		Speed 1;
		Health 100;
		Radius 20;
		Height 56;
		Mass 100;
		PainChance 255;
		Player.MaxHealth 100;
		player.soundclass "ImpPlayer";
		player.morphweapon "ImpArm";
		Player.ColorRange 0, 0;
		Player.DisplayName "Imp";
		Player.StartItem "ImpArm";
		Player.StartItem "MonsterMana", 1;
		Player.Face "STF";
		+NOSKIN;
	}

	States{
		Spawn:
			TROO AB 7;
			TROO A 0;
			Loop;
		See:
			TROO AABBCCDD 3;
			Loop;
		Missile:
			TROO EF 8;
			TROO G  6;
			Goto Spawn;
		Melee:
			TROO EF 8;
			TROO G  6;
			Goto Spawn;
		Pain:
			TROO H 4;
			TROO H 4 A_Pain;
			Goto Spawn;
		Death:
			TROO I 8 A_PlayerSkinCheck("AltSkinDeath");
			TROO J 8 A_PlayerScream;
			TROO K 6;
			TROO L 6 A_NoBlocking;
			TROO M -1;
			Stop;
		XDeath:
			TROO N 5 A_PlayerSkinCheck("AltSkinXDeath");
			TROO O 5 A_XScream;
			TROO P 5;
			TROO Q 5 A_NoBlocking;
			TROO RST 5;
			TROO U -1;
			Stop;
		AltSkinDeath:
			TROO I 8;
			TROO J 8 A_PlayerScream;
			TROO K 6;
			TROO L 6 A_NoBlocking;
			TROO M -1;
			Stop;
		AltSkinXDeath:
			TROO N 5 A_PlayerScream;
			TROO O 5 A_NoBlocking;
			TROO P 5;
			TROO Q 5 A_SkullPop;
			TROO RST 5;
			TROO U -1;
			Stop;
	}
}


class ImpArm : ImpscapeWeapon{
	default{
		radius 20;
		height 20;
		obituary "%o ate some k%'s IMP fire";
		inventory.pickupmessage "IMP-MAN";
		attacksound "imp/melee";
		weapon.selectionorder 2500;
		weapon.kickback 200;
		weapon.ammotype "MonsterMana";
		weapon.ammouse1 0;
		weapon.ammouse2 0;
		weapon.ammogive 1;
		weapon.SlotNumber 1;
	}

	States{
		Spawn:
			MANA A 2;
			loop;
		Deselect:
			IMPD A 1 A_Lower(12);
			LOOP;
		Select:
			IMPD A 1 A_Raise(12);
			LOOP;
		Ready:
			IMPD A 7 A_WeaponReady;
			IMPD A 0 A_GiveInventory ("MonsterMana",1);
			LOOP;
		Fire:
			IMPD A 0 A_JumpIfNoAmmo ("AltFire");
			//Aca salta a un estado depende cuantos FirePowerUp tengas.
			//Osea, cuantos mas FirePowerUp tengas, mas rapido disparas.
			//IMPD A 0 A_JumpIfInventory("FirePowerUp",4,"FireVeryFast");
			//IMPD A 0 A_JumpIfInventory("FirePowerUp",3,"FireFast");
			//IMPD A 0 A_JumpIfInventory("FirePowerUp",2,"FireMid");
			//IMPD A 0 A_JumpIfInventory("FirePowerUp",1,"FireSlow");
			//IMPD A 0 A_JumpIfInventory("FirePowerUp",0,"FireVerySlow");

			IMPD A 0 A_JumpIfInventory("FirePowerUp", 1, "FireVeryFast");
			IMPD A 0 A_Jump(256,"FireMid");

		//Normal
		FireMid:
			IMPD F 3;
			IMPD G 3 A_GunFlash;
			IMPD H 3 {
				if (CheckInventory("IMPPLAYER_FasterShooting",1,AAPTR_PLAYER1)){
					A_FireProjectile ("ImpPlayerBall_Faster");
				}
				else{
					A_FireProjectile ("ImpPlayerBall");
				}
			}
			IMPD I 3;
			TNT1 A 3 A_ReFire;
			IMPD J 3;
			Goto Ready;
			
		//Muy rapido
		FireVeryFast:
			IMPD F 2 A_TakeInventory("FirePowerUp", 1);
			IMPD G 2 A_GunFlash;
			IMPD H 2 {
				if (CheckInventory("IMPPLAYER_FasterShooting",1,AAPTR_PLAYER1)){
					A_FireProjectile ("ImpPlayerBall_Faster");
				}
				else{
					A_FireProjectile ("ImpPlayerBall");
				}
			}
			IMPD I 2;
			TNT1 A 2 A_ReFire;
			IMPD J 2;
			Goto Ready;

		Flash:
			TNT1 A 5;
			TNT1 A 5 bright A_Light1;
			TNT1 A 0 A_Light0;
			stop;

		AltFire:
			IMPD F 5;
		AltHold:
			IMPD A 0 A_JumpIfInventory("AltFireEnergy", 6, "AltFireCharged");
			IMPD A 0 A_JumpIfInventory("FirePowerUp", 1, "AltFaster");
			IMPD F 2 _A_JumpIfNotHold();
			IMPD F 2 _A_JumpIfNotHold();
		AltFaster:
			IMPD F 2 _A_JumpIfNotHold();
			IMPD F 2 _A_JumpIfNotHold();
			IMPD F 2 _A_JumpIfNotHold();
			IMPD G 2 {
				A_StopSound(CHAN_WEAPON);
				A_StartSound("ImpCharging", CHAN_WEAPON);
				A_GiveInventory("AltFireEnergy", 1);
			}
		AltFireCharged:
			IMPD F 1;
			IMPD G 1 A_ReFire;
		AltShoot:
			IMPD G 1 A_GunFlash;
			IMPD H 1 {
				while(CountInv("AltFireEnergy") > 0){
					if (CheckInventory("IMPPLAYER_FasterShooting",1,AAPTR_PLAYER1)){
						A_FireProjectile ("ImpPlayerBall_Faster",random(-15,15));
					}
					else{
						A_FireProjectile ("ImpPlayerBall",random(-15,15));
					}
					A_TakeInventory("AltFireEnergy", 1);
					A_TakeInventory("FirePowerUp", 1);
				}
			}
			IMPD I 1;
			IMPD J 5;
			Goto Ready;
		/*
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
		Normal:
			IMPD BC 5
			IMPD D 5 A_CustomPunch (7)
			IMPD E 5
			TNT1 A 5 A_ReFire
			IMPD J 5
			goto ready

		Berserked:
			IMPD BC 5
			IMPD D 5 A_CustomPunch (35)
			IMPD E 5
			TNT1 A 5 A_ReFire
			IMPD J 5
			goto ready
		*/

	}
}



class MonsterMana : ammo{
	default{
		inventory.amount 1;
		inventory.maxamount 1;
		inventory.icon "MANAB0";
		RENDERSTYLE "TRANSLUCENT";
		ALPHA 0.5;
	}
	
	states{
		Spawn:
			MANA ABC 5;
			loop;
	}
}

class AltFireEnergy : inventory{
	default{
		inventory.amount 1;
		inventory.maxamount 6;
		+COUNTITEM;
	}
	
	states{
		Spawn:
			MANA ABC 5;
			loop;
	}
}

class impplayerball : doomimpball{
	default{
		speed 18;
		damage 4;
	}
}

class impplayerball_faster : doomimpball{
	default{
		speed 24;
		damage 4;
	}
}

class IMPPLAYER_FasterShooting : Inventory{}